# Polimorfismo

En esta clase vamos a resolver un ejercicio aprovechando el _polimorfismo_.
Veamos todos los objetos involucrados en calcular el sueldo de Pepe...

### Objetivos de esta clase
1. Repaso de los conceptos b√°sicos vistos: objeto, referencia, mensaje (consulta o acci√≥n), m√©todo, atributo, asignaci√≥n
1. Plantear una soluci√≥n a un ejercicio aprovechando el _polimorfismo_ 
1. Conceptos de _modelado_
    1. 3 formas de _conocer un objeto_: global, atributo o par√°metro
    1. Decidir si _"me lo guardo o lo recibo?"_
    1. Objetos que representan cosas m√°s abstractas (a.k.a _objetos chetardos_)
1. _Metodolog√≠a_ propuesta
    1. ¬øCu√°l es el _requerimiento_?
    1. _¬øQu√© mensaje le voy a enviar a qu√© objeto (y con qu√© par√°metros)?_
        - [TDD](https://es.wikipedia.org/wiki/Desarrollo_guiado_por_pruebas) en la consola
        - _Perderle el miedo_ a que el programa explote (errores)
    1. ¬øM√©todo de _acci√≥n o consulta_?
    1. Implementaci√≥n

### Elementos del lenguaje
- `self`
- `if`
- `property`
- Comandos de la consola: `:r` `:rr`

### Apuntes te√≥ricos
1. [Referencias. Estado. Compartir objetos. Identidad.](https://docs.google.com/document/d/14092iRsXDXih8-q_0UEXIGRSQmGtxL9pay1VXX4ceJg/edit?usp=drive_web)
1. [Introducci√≥n a Polimorfismo.](https://docs.google.com/document/d/1X7Sz12e7rbVO1x7uMD7ECjZnT-chELx0ElTPmNvNURU/edit?usp=drive_web)
1. [Atributos como Propiedades.](https://docs.google.com/document/d/1wziW1YY-t94UUAUApydrt-OZ5roq1uY6DT6FduwNGx0/edit?usp=drive_web)

----

# El Sueldo de Pepe

> #TODO Ver qu√© versi√≥n del sueldo de pepe usar como enunciado...
> C√≥digo inicial (con las categor√≠as y m√©todos en blanco de pepe)?
> Pasar por la hoja en blanco?
> Requerimiento de sueldo base?
> O directamente el sueldo con bonos?
>
> El sistema debe ser extensible para nuevos empleados, nuevas categor√≠as de trabajo, y nuevos contratos en el futuro
> 
> 1. Hacer que pepe cambie de categor√≠a (property). Comienza en cadete pero puede cambiar a gerente, y volver a cadete, en cualquier momento.
> 2. Conocer el sueldo base de pepe (polimorfismo)
> 3. Conocer el sueldo (final) de pepe contemplando los posibles contratos
>         - B√°sico: es siempre de 1000
>         - Porcentual: es el 15 porciento del sueldo base del empleado
>         - Presentismo: Si el empleado no tuvo faltas es de 5000. Si tuvo 5 faltas o m√°s, es de 500. En otro caso, es igual al sueldo base

Presentamos el principio del enunciado (pepe, categor√≠as y bonos) y el primer requerimiento.
Despu√©s se abre un nuevo proyecto en blanco para comenzar...

# 1. C√≥mo encarar un ejercicio

#### Preguntas gatillo

> ¬øPor d√≥nde arrancamos?

Despu√©s de hacer un peque√±o debate para entrar en calor con el ejercicio y repasar los conceptos b√°sicos de objetos, proponemos nuestra metodolog√≠a.

### Metodolog√≠a para encarar un ejercicio

Esta es la metodolog√≠a que vamos a usar para resolver los ejercicios en toda la cursada:

1. ¬øCu√°l es el **requerimiento**? (uno, el primero, o el siguiente si ya hay algo resuelto)
    - Focalizamos en _una parte del problema_: "dividir y conquistar"
    - _Respuesta:_ Calcular el sueldo base de pepe
1. ¬ø**Qu√© mensaje** vamos a enviar a qu√© objeto (y con qu√© par√°metros)?
    - Pensamos primero en _c√≥mo vamos a usar_ los objetos (pensar en el _mensaje_ antes que en el _m√©todo_)
    - Armar una lista de _objetos candidatos_, si es necesario
    - Ac√° puede salir varias posibilidades, est√° bueno anotarlas todas para despu√©s definir
    - _Respuesta:_ `pepe.cambiarCategoria(gerente)`
1. Escribimos un **ejemplo concreto** del mensaje
    - Lo escribimos en la consola. _S√≠, ¬°antes de escribir el c√≥digo!_
    - Vemos que todo rompe porque no est√° implementado, pero _¬°no nos asustamos!_
1. **Acci√≥n o consulta**
    - Este es ¬øun mensaje de acci√≥n o de consulta?
      - Si es acci√≥n, _¬øqu√© esperamos que pase?_
      - Si es conuslta, _¬øqu√© esperamos que devuelva?_
    - _Respuesta:_ Es de acci√≥n, Pepe debe cambiar su categor√≠a de cadete a gerente.
1. **Implementaci√≥n**
    - Codear lo necesario para que el ejemplo se comporte como esperamos
    - Pensar si hay m√°s ejemplos para tener en cuenta
      - Implementar lo necesario para el nuevo ejemplo si es necesario
1. Repetir hasta terminar el ejercicio - **Iterar**

Proponemos esta metodolog√≠a porque:
- Focaliza en una parte del problema (y por el momento nos olvidamos del resto)
- Primero piensa en el qu√© antes que en el c√≥mo
  - O sea, primero pensamos en mensaje antes que en la implementaci√≥n del m√©todo
- Propone un objetivo claro
  - Al escribir el uso en la consola, ya tenemos en claro qu√© le falta al sistema
  - Le perdemos el miedo a que nos explote un programa en la cara (pr√°ctica de _debugging_)
  - El requerimiento se resuelve cuando la consola deje de fallarnos (los errores gu√≠an el desarrollo)
- En fin, propone una forma de trabajar **iterativa e incremental**

# 2. Manos a la obra

Ahora que tenemos un objetivo fijo:
```bash
> pepe.cambiarCategoria(gerente)
‚úì
```

> ¬øY qu√© tipo de objeto es `gerente`? ¬øUn String o un objeto nuestro? Guiar la soluci√≥n para definir los objetos `pepe`, `cadete` y `gerente` desde el principio.

Lo probamos para ver los errores e ir resolvi√©ndolos hasta que el programa se comporte como esperamos.

Si comenzamos con un archivo vac√≠o:
```wlk
// pepe.wlk
```

Y con el archivo cargado probamos en la consola:
```bash
pepe> pepe.cambiarCategoria(gerente)
‚úó Unknown reference gerente
```

Vemos que nos dice que no encuentra una referencia para `gerente`.
Lo que tiene sentido, porque no definimos ning√∫n objeto con ese nombre (de hecho, no definimos nada todav√≠a).

As√≠ que definimos un objeto que represente al gerente, en su forma m√°s b√°sica:
```wlk
// pepe.wlk

object gerente { }
```

Y _volvemos a probar_. Para eso hay varias opciones:
- Cerrar la consola actual y volver a levantarla :-1:
- Recargar la consola con `:r` y volver a tirar el mensaje (buscar con al fecha arriba ‚¨ÜÔ∏è) :+1_
- **Recargar y re-ejecutar** todo con `:rr` üëå

Vemos que ahora el mensaje de error cambi√≥:
```bash
pepe> pepe.cambiarCategoria(gerente)
‚úó Evaluation Error!
  wollok.lang.EvaluationError: Error: Could not resolve reference to pepe
```

Ahora, como es de esperar, falta `pepe`. Volvemos a hacer lo mismo, definiendo un objeto:

```wlk
// pepe.wlk

object pepe { }

object gerente { }
```

Con esto, ya tenemos los objetos que necesitamos definidos, y los podemos ver en el diagrama din√°mico:

<img width="405" height="270" alt="image" src="https://github.com/user-attachments/assets/d59a4b9b-7975-43cc-9596-293255f43884" />

As√≠ que _volvemos a probar_ en la consola. (Hay que meterles el **h√°bito** de la metodolog√≠a).

```bash
pepe> pepe.cambiarCategoria(gerente)
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodException: pepe does not understand cambiarCategoria(arg 0)
```

Ahora el error nos dice que _pepe no entiende el mensaje `cambiarCategoria(arg 0)`_.

#### Ac√° se pone interesante...

#### Preguntas gatillo

> ¬øQu√© significa que pepe no entienda el mensaje?

_Respuesta:_ que no tiene definido ning√∫n m√©todo con la _firma_ `cambiarCategoria/1`. (El `/1` es una forma de anotar que es un m√©todo que espera 1 par√°metro. Se puede tener m√©todos con el mismo nombre y distinta cantidad de par√°metros, pero no vamos a entrar en eso ahora).

As√≠ que debemos **definir un m√©todo** `method cambiarCategoria(unaCategoria)` en `pepe`.

Y, como hab√≠amos dicho, este debe ser un **m√©todo de acci√≥n**.
O sea, debe cambiar algo, pero no devolver nada...

> ¬øQu√© cosa debe cambiar este m√©todo?

_Respuesta:_ la categor√≠a de pepe.

El problema es que ahora pepe no tiene nada, es un objeto vac√≠o.
As√≠ que una decisi√≥n ac√° es decir que el objeto `pepe` tiene que conocer a otro objeto como su categor√≠a, o sea, que va a tener un **atributo `cateogria`**.

"Cambiar algo" en objetos es mover (_asignar_) un atributo, lo que ser√≠a re-apuntar una flecha en el diagrama din√°mico.
Sin embargo, nuestro programa todav√≠a no tiene ninguna flecha.

Pero no desesperemos, sigamos firme con la metodolog√≠a: escribamos c√≥mo ser√≠a el m√©todo, y veamos los errores.

```wlk
// pepe.wlk

object pepe { 

    method cambiarCategoria(unaCategoria) {
        categoria = unaCategoria
    }

}

object gerente { }
```

Ya con escribir ese c√≥digo nos sale un error de que el programa tiene una _referencia desconocida_:

<img width="634" height="310" alt="image" src="https://github.com/user-attachments/assets/1edd1092-a2b3-4c46-8859-175369095c52" />

O sea, el programa no sabe lo que significa el `categoria` a la izquierda de la asignaci√≥n.

Eso es porque falta definirle el atributo `var categoria`

```wlk
// pepe.wlk

object pepe { 

    var categoria

    method cambiarCategoria(unaCategoria) {
        categoria = unaCategoria
    }

}

object gerente { }
```

Ahora vemos que el error anterior desapareci√≥ pero aparece uno nuevo en la definici√≥n del atributo, diciendo que _los atributos de los objetos deben ser inicializados_:

<img width="634" height="357" alt="image" src="https://github.com/user-attachments/assets/b969b22c-4a18-484b-ab7f-2bf95f161af7" />

Esto es porque **una referencia siempre debe apuntar a un objeto**, y no puede nunca estar apuntando a "la nada".

Ac√° Wollok se est√° dando cuenta que al iniciar el programa el atributo `categoria` no tiene nada asignado, por ende no va a estar apuntando a ning√∫n objeto.

> Wollok fuerza a tener siempre objetos "listo para usarse", y considera que si no tiene un atributo inicializado entonces el objeto no va a poder funcionar como se espera. En Wollok existe el `null`, pero no lo vamos a ver ahora.

Por suerte, el enunciado nos aclara que pepe siempre comienza siendo cadete.

#### Preguntas gatillo

> ¬øC√≥mo hacemos para que pepe comienza siendo cadete?

_Respuesta:_ hay que **crear un nuevo objeto** `cadete` e **inicializar el atributo** `categoria` con √©l.


Al final el c√≥digo queda:

```wlk
object pepe { 

    var categoria = cadete

    method cambiarCategoria(unaCategoria) {
        categoria = unaCategoria
    }

}

object cadete { }

object gerente { }
```

Y _volvemos a probar en la consola_:

![Screen Recording 2026-01-12 at 17 10 44](https://github.com/user-attachments/assets/2b039980-c8b1-4074-b3e6-e44b3120f6be)

Vemos que pepe comienza apuntando a `cadete` como su categor√≠a, y lo podemos cambiar a `gerente`.
Tambi√©n podemos volver a la categor√≠a de `cadete` si le mandamos este por par√°metro al mensaje `cambiarCategoria`.

### ¬°Excelente! Primer punto completado üöÄ



## Propiedades - M√©todos que no se escriben

‚öΩ **Paramos la pelota** para meter algo de teor√≠a sobre lo que tenemos...

- El **enunciado** nos dice que Pepe puede ser cadete o genrente, y nos pide poder configurar su categor√≠a en cualquier momento
- Para **modelar** eso
    - creamos **3 objetos**: `pepe`, `cadete` y `gerente`
        > c√≠rculos en el diagrama din√°mico
    - y un **atributo** en `pepe` que _apunta a alguno de los otros 2 objetos_: `categoria`
        > flecha en el diagrama din√°mico
- Para poder **cambiar el atributo** de `pepe`: apuntar la flecha `categoria`
    - hay que enviarle un mensaje a `pepe`, por ejemplo: `pepe.cambiarCategoria(gerente)`
        - Esto se conoce como **encapsulamiento**
           > cada objeto es "due√±o" de sus atributos y solo ellos pueden _acceder_ a √©l, tanto para leerlo como para asignarlo.
    - y `pepe` debe implementar un m√©todo que asigne la nueva categor√≠a:
```wlk
    method cambiarCategoria(unaCategoria) {
        categoria = unaCategoria
    }
```

### Setters y Getters

> Como el paradigma de objetos propone tener _objetos encapsulados_, o sea, cada uno maneja su estado interno (atributos), los m√©todos que solo cambian o retornan alg√∫n atributo son **muy comunes** en los programas.

Vamos a jugar un poco con el c√≥digo: 
1. Abrimos una **nueva sesi√≥n** de la consola 
1. **Cerramos** el diagrama din√°mico (en realidad conviene solo ocultarlo, para poder verlo r√°pido durante la explicaci√≥n)
1. Y le cambiamos la categor√≠a a Pepe

```bash
pepe> pepe.cambiarCategoria(gerente)
‚úì 
```

#### Preguntas gatillo

> Si no vemos el diagrama ¬øc√≥mo sabemos que Pepe cambi√≥ de su categor√≠a?

_Respuesta:_ hay que _mandarle un mensaje_ pregunt√°ndole su categor√≠a: `pepe.obtenerCategoria()`

> ¬øEse es un mensaje de acci√≥n o consulta?

_Respuesta:_ consulta. _Debe retornar_ su categor√≠a, o sea, el objeto apuntado por su atributo

Ac√° podemos mostrar el diagrama r√°pidamente para entender que queremos retornar el objeto apuntado (y no un string u otra cosa que lo represente).

Tambi√©n podemos evaluar directamente esos objetos para que se entienda:

```bash
pepe> cadete
‚úì cadete
pepe> gerente
‚úì gerente
```

Pero si lo consultamos ahora nos da un error diciendo que _no entiende el mensaje_:

```bash
pepe> pepe.obtenerCategoria()
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodException: pepe does not understand obtenerCategoria()
```

**Repasamos c√≥mo seguir** a partir de ac√°:

- No entiende el mensaje porque no implementa un m√©todo
- Hay que definir un m√©todo en el objeto `pepe` que se llame `obtenerCategoria/0`
- Ese m√©todo debe retornar el atributo `categoria`

Lo escribimos (ac√° solo se presenta el c√≥digo de `pepe`, el resto queda igual)

```wlk
object pepe { 

    var categoria = cadete

    method cambiarCategoria(unaCategoria) {
        categoria = unaCategoria
    }

    method obtenerCategoria() = categoria

}
```


(podr√≠a ser su versi√≥n con `{ return ... }`)

Y reevaluamos todo con `:rr` y vemos que ya no explota nada.

Ahora podemos intercalar los _cambiar_ y _obtener_ (podemos limpiar la consola con `ctrl + k` o `ctrl + l` o algo similar):

```bash
pepe> pepe.cambiarCategoria(cadete)
‚úì 
pepe> pepe.obtenerCategoria()
‚úì cadete
pepe> pepe.cambiarCategoria(gerente)
‚úì 
pepe> pepe.obtenerCategoria()
‚úì gerente
```

Ac√° explicamos c√≥mo se conocen (por sus nombres en ingl√©s) estos m√©todos:

- El m√©todo que cambia un atributo por un par√°metro se conocen como _setter_: `cambiarCategoria/1`, `cambiarEnergia/1`, `cambiarAlgo/1`
- El m√©todo que retorna un atributo (y no tiene par√°metros) se conocen como _getter_: `obtenerCategoria/0`, `obtenerEnergia/0`, `obtenerAlgo/0`
- Los lenguajes de programaci√≥n (y su comunidad) generalmente definen una forma para el nombre de estos m√©todos
    - por ej: `setCategoria/1` y `getCategoria/0`
    - esto se conoce como **convenci√≥n**: una forma de nombrar alg√∫n elemento del programa en el que _se ponen de acuerdo la comunidad_

La convenci√≥n de Wollok para los getters y setters es con el mismo nombre del atributo (sin prefijo): `categoria/1` y `categoria/0`

**Modificamos el c√≥digo**:

```wlk
object pepe { 

    var categoria = cadete

    method categoria(unaCategoria) {
        categoria = unaCategoria
    }

    method categoria() = categoria

}
```

Levantamos una nueva **consola** y volvemos a hacer las consultas con los nuevos nombres:

```bash
pepe> pepe.categoria(gerente)
‚úì 
pepe> pepe.categoria()
‚úì gerente
pepe> pepe.categoria(cadete)
‚úì 
pepe> pepe.categoria()
‚úì cadete
```

#### Atenci√≥n

Resaltar que ahora en el c√≥digo de pepe hay _3 cosas **distintas**_ que se llaman "categoria":

1. Definici√≥n del _atributo_: `var categoria`
1. M√©todo _setter_: `method categoria(unaCategoria)`
1. M√©todo _getter_: `method categoria()`

- Los atributos y m√©todos se definen y usan de forma distintas
    - Los atributos se definen con `var` o `const`, mientras que los m√©todos con `method`
    - Los atributos se usan como cualquier referencia, mientras que los m√©todos a trav√©s de envios de mensajes (despu√©s de un `.`)
- Los getter y setter tienen distinta firma, debido a sus par√°metros
    - Los getters no esperan par√°metro y son de consulta: `categoria/0`
    - Los setters esperan el nuevo objeto y son de acci√≥n: `categoria/1`
    - Como tienen distintos par√°metros, se invoca uno u otro m√©todo en base a los par√°metros que tiene el mensaje


### Atributos `property`

Como estos m√©todos son comunes en los lenguajes orientado a objetos, muchos traen una forma de declararlos f√°cilmente.

En Wollok, se puede usar el modificador `property` en la definici√≥n de un atributo para crear sus getters y setters.

El c√≥digo de `pepe` quedar√≠a as√≠ (dejo la versi√≥n con comentarios para los estudiantes, pero se sugiere probarlo sin los m√©todos escritos para que se entienda c√≥mo funciona):

```wlk
object pepe { 

    var property categoria = "asd."

/*
Este c√≥digo se crea con el "property" del atributo categoria

Est√° comentado para recordar lo que hace, pero no hace falta escribirlos

GETTER:
    method categoria(unaCategoria) {
        categoria = unaCategoria
    }

SETTER:
    method categoria() = categoria
*/
}
```

Y repetimos las consultas para probar que anda (probar es **siempre** mucho muy importante):

```bash
pepe> :rr
‚úì Reloading environment
pepe> pepe.categoria(gerente)
‚úì 
pepe> pepe.categoria()
‚úì gerente
pepe> pepe.categoria(cadete)
‚úì 
pepe> pepe.categoria()
‚úì cadete
```


### Posibles discusiones

- `var property` vs `const property`
- ¬øQu√© pasa si escribo un m√©todo getter o setter usando `property`?
- Ahora tengo un getter y antes no, ¬øes eso mejor?


## C√≥digo final del primer punto

El c√≥digo final para el primer punto queda en estas pocas l√≠neas

```wlk
object pepe { 

    var property categoria = cadete

}

object cadete { }

object gerente { }
```

Continuemos con el siguiente...



# 3. Segundo requerimiento

Ahora que ya conocemos la metodolog√≠a, vamos a aplicarla para el segundo punto...

<img width="210" height="190" alt="image" src="https://github.com/user-attachments/assets/60336cd7-92dd-4342-a4ca-a4f98ee9ee84" />

> ¬øCu√°l es el requerimiento?

_Respuesta:_ conocer el sueldo base de pepe

> ¬øQu√© mensaje le vamos a enviar a qu√© objeto?

_Respuesta:_ `pepe.sueldoBase()`

> ¬øEs de acci√≥n o de consulta?

_Respuesta:_ de consulta

> Ejemplo de consulta

_Respuesta:_ siendo cadete esperar√≠amos

```bash
> pepe.sueldoBase()
1500
```

#### Comenzamos

Probamos la consulta en la **consola**

```bash
pepe> pepe.sueldoBase()
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodExcegption: pepe does not understand sueldoBase()
```

Y **leemos** el error: _pepe no entiende el mensaje `sueldoBase()`_

> ¬øC√≥mo se soluciona?

_Respuesta:_ implementando el m√©todo (de consulta) en `pepe`

El enunciado dice que el sueldo base es siempre
- 1000
- m√°s un extra dependiendo de la categor√≠a
    - 500 para cadete
    - 1500 para gerente

Escribimos la parte f√°cil:

```wlk
object pepe { 

    var property categoria = cadete

    method sueldoBase() = 1000 + ...

}
```

#### Preguntas gatillo

> ¬øC√≥mo calculamos el extra de la categor√≠a?

_Respuesta:_ si el extra _depende_ de la categor√≠a, **¬°lo mejor es pregunt√°rselo a ella!**

```wlk
method sueldoBase() = 1000 + categoria.extra()
```

Terminamos de escribir el m√©todo y **volvemos a probar**:

```wlk
pepe> :rr
‚úì Reloading environment
pepe> pepe.sueldoBase()
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodException: cadete does not understand extra()
    at pepe.pepe.sueldoBase() [pepe.wlk:5]
```

Ahora nos dice que _cadete no entiende el mensaje `extra()`_

Tambi√©n nos muestra el _stack trace_, o sea, d√≥nde se produjo el error: `[pepe.wlk:5]` (con `ctrl + click` nos navega al m√©todo que env√≠a el mensaje)

> ¬øC√≥mo se soluciona?

_Respuesta:_ implementando el m√©todo en el objeto `cadete`

> ¬øEs de acci√≥n o de consulta?

_Respuesta:_ consulta. Deber√≠a retornar `500`

Lo escribimos

```wlk
object cadete { 
    method extra() = 500
}
```

Y volvemos a probar

```bash
pepe> :rr
‚úì Reloading environment
pepe> pepe.sueldoBase()
‚úì 1500
```

### ¬°Funciona! ¬°Bravo!

#### Preguntas gatillo

> ¬øYa terminamos?

_Respuesta:_ no. Todav√≠a falta probar siendo gerente.

Pensar todos los casos de prueba es algo _importante_ que queremos ejercitar y lo vamos a estar practicando la clase que viene

> ¬øY c√≥mo probamos ese caso?

_Respuesta:_ hay que cambiarle la categoria a gerente y preguntarle el sueldo base

```bash
pepe> pepe.categoria(gerente)
‚úì 
pepe> pepe.sueldoBase()
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodException: gerente does not understand extra()
    at pepe.pepe.sueldoBase() [pepe.wlk:5]
```

Nos dice que _gerente no entiende el mensaje `extra()`_. 
¬°Que bueno! que probamos este caso, nos falta implementar parte del c√≥digo a√∫n.

Implementamos el m√©todo que falta:

```wlk
object gerente { 
    method extra() = 1500
}
```

Y **volvemos a probar** todo

```bash
pepe> :rr
‚úì Reloading environment
pepe> pepe.sueldoBase()
‚úì 1500
pepe> pepe.categoria(gerente)
‚úì 
pepe> pepe.sueldoBase()
‚úì 2500
```

#### ¬°Genial! Ahora s√≠ ya terminamos


## Polimorfismo de Categor√≠as

‚öΩ **Paramos la pelota** nuevamente para analizar lo que hicimos...

La clave de este punto est√° en _c√≥mo calculamos el monto extra en base a la categor√≠a_:

```wlk
method sueldoBase() = 1000 + categoria.extra()
```

- **Ignorancia**: Para calcular el sueldo base, pepe _**no** pregunta qu√© categor√≠a tiene_
    > No pregunto qui√©n es, directamente pido lo que necesito

- **Delegaci√≥n**: En vez de eso, delega la responsabilidad de "calcular" el extra en la categor√≠a mand√°ndole directamente un mensaje
    > Si el extra depende de la categor√≠a, lo mejor es preguntarle a ella

- **Responsabilidad**: Es responsabilidad de cada categor√≠a el saber cu√°l es su extra (en base a qui√©n representa el objeto)
    > ¬øQui√©n tiene la responsabilidad de... (calcular el extra)?

#### Polimorfismo

Todas esas ideas se combinan para dise√±ar una soluci√≥n con _polimorfismo_, uno de los pilares de la programaci√≥n orientada a objetos

La clave est√° en que **`pepe` usa indistintamente a cualquiera de sus categor√≠as**

Beneficios de este dise√±o:

- _Bajo acoplamiento_: `pepe` no sabe c√≥mo funcionan las categor√≠as por dentro, las categor√≠as tampoco saben lo que hace `pepe`. Solamente se comunican a trav√©s de un _contrato_ que es el mensaje `categoria.extra()`
- _Extensividad_: es "f√°cil" entender c√≥mo agregar m√°s categor√≠as (siempre que entiendan el mensaje `extra/0`, pero sino tambi√©n es "f√°cil" adaptar el mensaje a lo que se necesite)

#### Interfaz

> Hablamos mucho sobre "categor√≠as" pero nunca escribimos una definici√≥n sobre eso. Definimos a `gerente` y `cadete`, pero nada relacionado con "categor√≠a". (El √∫nico lugar es el nombre del atributo en `pepe`, lo que habla bien sobre nuestra _expresividad_)

Podemos decir que **la `categoria` es c√≥mo `pepe` conoce al objeto a cual preguntarle el `extra`**.
Este conjunto de mensajes por el cual un objeto se comunica con otro se llama **interfaz**

> En este programa, la interfaz `categoria` est√° constitu√≠da solamente por el mensaje `extra/0`


# 4. √öltimo requerimiento


> ¬øCu√°l es el requerimiento?

_Respuesta:_ conocer el sueldo (final) de pepe teniendo en cuenta su contrato

> ¬øQu√© mensaje le vamos a enviar a qu√© objeto?

_Respuesta:_ `pepe.sueldo()`

> ¬øEs de acci√≥n o de consulta?

_Respuesta:_ de consulta

> Ejemplo de consulta (comenzando por lo m√°s f√°cil)

_Respuesta:_ siendo cadete (1500) y con un contrato b√°sico (1000) esperar√≠amos

```bash
> pepe.sueldo()
2500
```

Lo escribimos en la consola para tener el ya conocido error de _no entiende el mensaje_ y ponernos a codear...

## "Yo mismo"

Este punto tiene como objetivo introducir la referencia de todo objeto a s√≠ mismo: `self`

Sin embargo, podemos hacer una soluci√≥n con las herramientas ya presentadas:

```wlk
object pepe { 

    var property categoria = cadete

    method sueldo() = pepe.sueldoBase()

    method sueldoBase() = 1000 + categoria.extra()

}
```

Y, como siempre, lo probamos:

```bash
pepe> :rr
‚úì Reloading environment
> pepe.sueldo()
1500
```

#### Funciona!

Igual el IDE est√° avisando de algo:

<img width="508" height="244" alt="image" src="https://github.com/user-attachments/assets/8579ac3b-4e11-4baf-9d72-97e837a7eafc" />

### Presentar `self`

> Es una referencia que conocen todos los objetos y que apunta a s√≠ mismos

Cambiar el c√≥digo para arreglar el aviso y **probar que la consola se sigue comportando igual**

```wlk
method sueldo() = self.sueldoBase()
```

En el diagrama din√°mico, las referencias a `self` no aparecen, pero podemos _forzarlas_ definiendo un atributo (`yoMismo`) inicializado en `self` en _todos los objetos_

```wlk
object pepe { 
    const yoMismo = self

    var property categoria = cadete

    method sueldo() = self.sueldoBase()

    method sueldoBase() = 1000 + categoria.extra()

}

object cadete { 
    const yoMismo = self

    method extra() = 500
}

object gerente { 
    const yoMismo = self

    method extra() = 1500
}
```

<img width="532" height="160" alt="image" src="https://github.com/user-attachments/assets/d417acd2-2949-4220-a7d3-fa2f9ce4dda4" />

Ac√° se visualiza:

- El **"rulo"** que significa una referencia a "s√≠ mismo"
- En un programa prueden existir **muchos atributos con el mismo nombre**, siempre que _salgan_ de objetos distintos
- En el c√≥digo, la paralabra `self` significa algo distinto **depende en qu√© objeto est√© escrito**

Comentar (o borrar) los atributos `yoMismo` para seguir con el ejercicio

### Posibles discusiones

Algunas aristas que puede tomar esta clase y tocan conceptos importantes

<details>
<summary>Repetir c√≥digo</summary>

Este punto se puede solucionar sin `self` copy-pasteando la definici√≥n de `sueldoBase()`

```wlk
method sueldo() = 1000 + categoria.extra()

method sueldoBase() = 1000 + categoria.extra()
```

Vamos a **evitar repetir l√≥gica** a toda costa, y fomentar soluciones con **reutilizaci√≥n de l√≥gica**

> Si ya existe un componente (m√©todo) que resuelve parte de mi problema, lo mejor es aprovecharlo

</details>

<details>
<summary>Romper el requerimiento anterior</summary>

Tambi√©n se podr√≠a _renombrar_ el m√©todo anterior para que se llame `sueldo()` y continuar desde ah√≠ (de esa forma se evita el c√≥digo repetido)

```wlk
method sueldo() = 1000 + categoria.extra()
```

Hacer notar que esto rompe con el requierimiento anterior, y que adem√°s es contraproducente: **perdemos una abstracci√≥n que ya existe**

</details>

<details>
<summary>Romper la sintaxis</summary>

Un error com√∫n al comenzar la POO es _olvidarse el receptor_ de un mensaje

```wlk
method sueldo() = sueldoBase()

method sueldoBase() = 1000 + categoria.extra()
```

Esto marca un error en el IDE

<img width="543" height="109" alt="image" src="https://github.com/user-attachments/assets/a07c5611-3765-4e15-a606-9655018a8dc2" />

A√∫n as√≠ es bueno recordar que en este paradigma (puro), todas las computaciones suceden a partir de enviar un mensaje.

> Un mensaje siempre tiene (necesita de) un **objeto** receptor: `objeto.mensaje()`

</details>


## Segundo polimorfismo

#### Volver a al problema

Esper√°bamos que nos diera 

```bash
> pepe.sueldo()
2500
```

Pero ahora nos da

```bash
pepe> :rr
‚úì Reloading environment
> pepe.sueldo()
1500
```

#### _Hardcodear_ una soluci√≥n

> Si al resultado le falta 1000, se lo sumamos...

```wlk
method sueldo() = self.sueldoBase() + 1000
```

Y probamos

```bash
pepe> :rr
‚úì Reloading environment
> pepe.sueldo()
2500
```

#### Excelente...

> o bueno, no tanto... esta implementaci√≥n fue para salir del paso, pero no _resuelve_ el enunciado


### Los contratos

Ac√° buscamos introducir a los distintos _contratos_ de forma **similar a las _categor√≠as_**

- Entender que Pepe debe estar _asociado_ a una de los contratos: b√°sico, porcentual o presentismo
- Se debe poder _elegir (configurar)_ qu√© contrato tiene Pepe
- El sueldo se debe _calcular a partir del contrato_ (y categor√≠a) configurado(a)

Para eso 

1. Definimos **3 nuevos objetos**

```wlk
object basico {

}

object porcentual {
    
}

object presentismo {
    
}
```

2. Agregamos un **nuevo atributo** en `pepe`

```wlk
var property contrato = basico
```

3. Le **mandamos un mensaje** al `contrato` para calcular el sueldo

```wlk
method sueldo() = self.sueldoBase() + contrato.remuneracion()
```

Probamos qu√© onda

```bash
pepe> :rr
‚úì Reloading environment
> pepe.sueldo()
‚úó Evaluation Error!
  wollok.lang.MessageNotUnderstoodException: basico does not understand remuneracion()
    at pepe.pepe.sueldo() [pepe.wlk:5]
```

Y **leemos el error**

> `basico` no entiende el mensaje `remuneracion()`

> ¬øEs de acci√≥n o de consulta?

_Respuesta:_ de consulta

Implementamos el m√©todo con el c√≥digo necesario para que la prueba funcione como antes

```wlk
object basico {
    method remuneracion() = 1000
}
```

Y probamos

```bash
pepe> :rr
‚úì Reloading environment
> pepe.sueldo()
2500
```

#### Al repetir estos pasos buscamos

- que se adopte la _metodolog√≠a_ de trabajo
- fortalecer el dise√±o que aprovecha el _polimorfismo_
- mostrar c√≥mo el programa crece de forma _iterativa e incremental_


### Porcentual, ¬ønos conocemos?

> Una vez implementado el contrato m√°s f√°cil, continuamos con el siguiente: `porcentual`

Este punto plantea **el problema de c√≥mo el `porcentual` conoce a `pepe`**

El objetivo es introducir las 3 formas de conocer un objeto desde otro:
- global: la referencia est√° _hardcodeada_ en el c√≥digo
- atributo: la referencia est√° en un _atributo del objeto_
- param√©trico: la referencia viene por _par√°metro_

Comenzamos a implementar el m√©todo 

#### Global

La primera forma de implementar este punto suele ser la _global_

```wlk
object porcentual {
    const porcentaje = 15
    method remuneracion() = pepe.sueldoBase() * porcentaje / 100
}
```

Probamos, configurando a `pepe` antes

```bash
pepe> pepe.contrato(porcentual)
‚úì 
pepe> pepe.sueldo()
‚úì 1725
```

Joya!

Adem√°s vemos c√≥mo queda el diagrama

<img width="726" height="325" alt="image" src="https://github.com/user-attachments/assets/de7960af-dae0-4e2a-9bb1-613ecc3dba7b" />

Marcamos algunas caracter√≠sticas de esta soluci√≥n:
- Referencias unilaterales
- La referencia est√° en el c√≥digo del m√©todo


#### Atributo


#### Par√°metro



- Porcentual: pasar self por par√°metro (discutir sobre el sueldo base)
- Pasar por referencia: no hay objetos m√°s pesados que otros


### Dise√±ar es tomar decisiones

- Presentismo: qui√©n se guarda las faltas?
